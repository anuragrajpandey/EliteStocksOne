import java.util.Properties
import java.io.FileInputStream
import java.io.File

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

// Release signing is loaded from an untracked local file. Google Play App
// Signing should own the distribution key; this is only the upload key.
// Local Community releases use a separate private key without replacing the
// Play upload configuration at android/key.properties.
val keystorePropertiesFile = providers.gradleProperty("eliteStocksOneSigningPropertiesFile").orNull
    ?.let(::File) ?: rootProject.file("key.properties")
val hasKeystore = keystorePropertiesFile.exists()
val keystoreProperties = Properties()
if (hasKeystore) keystoreProperties.load(FileInputStream(keystorePropertiesFile))
val requiresReleaseSigning = gradle.startParameter.taskNames.any {
    it.contains("release", ignoreCase = true)
}
val isCommunityBuild = providers.gradleProperty("eliteStocksOneCommunityBuild")
    .orNull
    ?.toBooleanStrictOrNull() == true
if (requiresReleaseSigning && !hasKeystore) {
    throw GradleException(
        "Release Android artifacts require android/key.properties and a private signing keystore."
    )
}

android {
    namespace = "com.anuragrajpandey.elitestocksone"
    compileSdk = 36
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId = if (isCommunityBuild) {
            "com.anuragrajpandey.elitestocksone.community"
        } else {
            "com.anuragrajpandey.elitestocksone"
        }
        resValue(
            "string",
            "app_name",
            if (isCommunityBuild) "EliteStocks One" else "EliteStocks One",
        )
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = 36
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        if (hasKeystore) {
            create("release") {
                keyAlias = keystoreProperties["keyAlias"] as String
                keyPassword = keystoreProperties["keyPassword"] as String
                storeFile = file(keystoreProperties["storeFile"] as String)
                storePassword = keystoreProperties["storePassword"] as String
                (keystoreProperties["storeType"] as? String)?.let {
                    storeType = it
                }
            }
        }
    }

    buildTypes {
        release {
            // Never ship a release artifact signed with Android's public debug key.
            signingConfig = if (hasKeystore) signingConfigs.getByName("release") else null
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    // Native Android/TV compatibility engine. Lumen keeps media_kit for its
    // cross-platform player and can hand difficult streams to Media3.
    implementation("androidx.media3:media3-exoplayer:1.10.0")
    implementation("androidx.media3:media3-exoplayer-hls:1.10.0")
    implementation("androidx.media3:media3-ui:1.10.0")
    testImplementation("junit:junit:4.13.2")
}
